(ns paneer.engine
  (:require [clojure.string :as str]))

(defn- column-from-def
  [{:keys [type col-name options]}]
  (let [options (str/join " " (map (comp #(str/replace % "-" " ") 
                                         str/upper-case 
                                         name) options))]
    (str "\"" col-name "\" " type " " options)))

(defn- columns-from-defs
  [columns]
  (str/join ", " (if-not (every? string? columns)
                   (map column-from-def columns)
                   columns))) 

(defn- command-from-type
  [command if-exists]
  (cond
    (= command :create) (fn [tbl {:keys [columns]}] 
                       (str "CREATE TABLE " (when if-exists "IF NOT EXISTS ") 
                            "\"" tbl "\" (" (columns-from-defs columns) ");"))
    (= command :alter-rename) (fn [tbl {:keys [new-table]}] 
                             (str "ALTER TABLE " (when if-exists "IF EXISTS ") 
                                  "\"" tbl "\" RENAME TO \"" new-table "\";"))
    (= command :alter-create-column) (fn [tbl {:keys [columns]}]
                                    (str "ALTER TABLE " (when if-exists "IF EXISTS ")
                                        "\"" tbl "\" ADD COLUMN " 
                                         (columns-from-defs columns) ";"))
    (= command :alter-drop-column) (fn [tbl {:keys [columns]}]
                                     (str "ALTER TABLE " (when if-exists "IF EXISTS ")
                                          "\"" tbl "\" DROP COLUMN "
                                         (when if-exists "IF EXISTS ") "\"" (first columns) "\";"))
    (= command :alter-rename-column) (fn [tbl {:keys [columns]}]
                                       (let [[old-name new-name] columns]
                                         (str "ALTER TABLE " (when if-exists "IF EXISTS ")
                                              "\"" tbl "\" RENAME COLUMN \""
                                              old-name "\" TO \"" new-name "\";")))
    (= command :drop) (fn [tbl & rest] (str "DROP TABLE " (when if-exists "IF EXISTS ")
                                            "\"" tbl "\";"))))

(defn make-query
  "Accepts a map generated by the functions in paneer.core in the form
    :command - :create, :alter-rename, :alter-create-column, :alter-drop-column, 
      :alter-rename-column or :drop specifies the type of actions on the table
    :if-exists - true of false if the action is the if-exists (or if-not-exists 
      in the case of create) version. If-exists is used in an alter command, it
      will be applied lower in the command as well. (e.g. ALTER TABLE IF EXISTS
      ...DROP COLUMN IF EXISTS ...;)
    :table - string name of the table to take action on
    :new-table - string name of table on :alter-rename
    :columns - Vector of definitions of the columns that will be acted upon. For
      create actions and alter/create actions columns will be maps of the column 
      defintion. For alter rename or alter constrain actions columns are just 
      the string name of the column. And the Action is specified under the :alter 
      keyword"
  [{:keys [command if-exists table] :as opts}]
  ((command-from-type command if-exists) table opts))